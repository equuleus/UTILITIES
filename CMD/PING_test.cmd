@ECHO OFF
SETLOCAL ENABLEEXTENSIONS
SETLOCAL ENABLEDELAYEDEXPANSION
CLS

SET PARAMETER=%1
SET LOG_FILE_PATH=%~dp0
SET LOG_FILE_NAME=%~n0.log
SET PING_HOST=127.0.0.1
SET PING_PACKETS=3

IF "%PARAMETER%" NEQ "" (
	SET LOG_FILE_NAME=%PARAMETER%.log
	SET PING_HOST=%PARAMETER%
)

IF "%LOG_FILE_PATH%" NEQ "" (
	IF EXIST "%LOG_FILE_PATH%" (
		IF "%LOG_FILE_NAME%" NEQ "" (
			IF NOT EXIST "%LOG_FILE_PATH%%LOG_FILE_NAME%" (
				TYPE>"%LOG_FILE_PATH%%LOG_FILE_NAME%"2>NUL
			)
			SET LOG=%LOG_FILE_PATH%%LOG_FILE_NAME%
		)
	)
)
:LOOP
	SET DATETIME=%DATE% %TIME%
rem	FOR /F "skip=%PING_PACKETS% tokens=*" %%X IN ('PING %PING_HOST% -n %PING_PACKETS% -w 10 ^| FIND "="') DO (
rem	FOR /F "tokens=*" %%X IN ('PING %PING_HOST% -n %PING_PACKETS% -w 10 ^| FIND "="') DO (
	FOR /F "tokens=*" %%X IN ('PING %PING_HOST% -n %PING_PACKETS% -w 10') DO (
		SET STRING=%%X
		SET ACTION=

		IF "!STRING:~0,5!" EQU "Reply" SET ACTION=REPLY
		IF "!STRING:~0,5!" EQU "Ответ" SET ACTION=REPLY
		IF "!STRING:~0,18!" EQU "Request timed out." SET ACTION=TIMEOUT
		IF "!STRING:~0,39!" EQU "Превышен интервал ожидания для запроса." SET ACTION=TIMEOUT
		IF "!STRING:~0,7!" EQU "Pinging" SET ACTION=PING
		IF "!STRING:~0,16!" EQU "Обмен пакетами с" SET ACTION=PING
		IF "!STRING:~0,19!" EQU "Ping statistics for" SET ACTION=STATISTICS
		IF "!STRING:~0,19!" EQU "Статистика Ping для" SET ACTION=STATISTICS
		IF "!STRING:~0,8!" EQU "Packets:" SET ACTION=PACKETS
		IF "!STRING:~0,8!" EQU "Пакетов:" SET ACTION=PACKETS
		IF "!STRING:~0,1!" EQU "(" SET ACTION=DESCRIPTION
		IF "!STRING:~0,46!" EQU "Approximate round trip times in milli-seconds:" SET ACTION=DESCRIPTION
		IF "!STRING:~0,43!" EQU "Приблизительное время приема-передачи в мс:" SET ACTION=DESCRIPTION
		IF "!STRING:~0,7!" EQU "Minimum" SET ACTION=TIME
		IF "!STRING:~0,11!" EQU "Минимальное" SET ACTION=TIME

		IF "!ACTION!" EQU "TIMEOUT" (
			SET TEXT=%DATETIME%	IP: %PING_HOST%	ALERT: TIMEOUT!
			IF "%LOG%" NEQ "" ECHO !TEXT! >>"%LOG%"
			ECHO !TEXT!
		) ELSE IF "!ACTION!" EQU "PACKETS" (
			FOR /F "tokens=1-10 delims= " %%A IN ("!STRING!") DO (
				SET PING_PAKETS_SEND=%%D
				IF "!PING_PAKETS_SEND:~-1!" EQU "," SET PING_PAKETS_SEND=!PING_PAKETS_SEND:~0,-1!
				SET PING_PAKETS_RECIEVED=%%G
				IF "!PING_PAKETS_RECIEVED:~-1!" EQU "," SET PING_PAKETS_RECIEVED=!PING_PAKETS_RECIEVED:~0,-1!
				SET PING_PAKETS_LOST=%%J
			)
			SET TEXT=%DATETIME%	IP: %PING_HOST%	Packets [SEND / RECIEVED / LOST]:	!PING_PAKETS_SEND! / !PING_PAKETS_RECIEVED! / !PING_PAKETS_LOST!	["!STRING!"]
			IF "%LOG%" NEQ "" ECHO !TEXT! >>"%LOG%"
			ECHO !TEXT!
		) ELSE IF "!ACTION!" EQU "TIME" (
			FOR /F "tokens=1-10 delims= " %%A IN ("!STRING!") DO (
				SET PING_TIME_MINIMUM=%%C
				IF "!PING_TIME_MINIMUM:~-1!" EQU "," SET PING_TIME_MINIMUM=!PING_TIME_MINIMUM:~0,-1!
				IF "!PING_TIME_MINIMUM:~-4!" EQU "мсек" (
					SET PARSE_BUG=TRUE
				) ELSE (
					SET PARSE_BUG=FALSE
				)
				IF "!PARSE_BUG!" EQU "TRUE" (
rem					SET TEXT=%DATETIME%	PARSE BUG DETECTED
rem					IF "%LOG%" NEQ "" ECHO !TEXT! >>"%LOG%"
rem					ECHO !TEXT!
					SET PING_TIME_MINIMUM=!PING_TIME_MINIMUM:~0,-4!
					SET PING_TIME_MAXIMUM=%%F
					SET PING_TIME_AVERAGE=%%J
				) ELSE (
					SET PING_TIME_MINIMUM=%%B
					SET PING_TIME_MAXIMUM=%%E
					SET PING_TIME_AVERAGE=%%I
					IF "!PING_TIME_MINIMUM:~-1!" EQU "," SET PING_TIME_MINIMUM=!PING_TIME_MINIMUM:~0,-1!
					IF "!PING_TIME_MAXIMUM:~-1!" EQU "," SET PING_TIME_MAXIMUM=!PING_TIME_MAXIMUM:~0,-1!
				)
			)
			SET TEXT=%DATETIME%	IP: %PING_HOST%	Time [MINIMUM / MAXIMUM / AVERAGE]:	!PING_TIME_MINIMUM! / !PING_TIME_MAXIMUM! / !PING_TIME_AVERAGE!	["!STRING!"]
			IF "%LOG%" NEQ "" ECHO !TEXT! >>"%LOG%"
			ECHO !TEXT!
		)
	)
	IF "!PING_PAKETS_LOST!" NEQ "0" (
		SET TEXT=%DATETIME%	IP: %PING_HOST%	ALERT: PACKET LOSS!
		IF "%LOG%" NEQ "" ECHO !TEXT! >>"%LOG%"
		ECHO !TEXT!
	)
	SET PING_PAKETS_SEND=
	SET PING_PAKETS_RECIEVED=
	SET PING_PAKETS_LOST=
	SET PING_TIME_MINIMUM=
	SET PING_TIME_MAXIMUM=
	SET PING_TIME_AVERAGE=
	SET PARSE_BUG=
	SET TEXT=
	TIMEOUT /t 10 > NUL
GOTO LOOP

:END
GOTO :EOF
